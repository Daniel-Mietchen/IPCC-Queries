#defaultView:Map
PREFIX wdt: <https://kg-ipclimatec-reports.wikibase.cloud/prop/direct/>
PREFIX wd:  <https://kg-ipclimatec-reports.wikibase.cloud/entity/>
PREFIX p:   <https://kg-ipclimatec-reports.wikibase.cloud/prop/>
PREFIX pq:  <https://kg-ipclimatec-reports.wikibase.cloud/prop/qualifier/>
PREFIX ps:  <https://kg-ipclimatec-reports.wikibase.cloud/prop/statement/>
PREFIX psv: <https://kg-ipclimatec-reports.wikibase.cloud/prop/statement/value/>

PREFIX rwdt: <http://www.wikidata.org/prop/direct/>

SELECT ?paragraph ?paragraphLabel ?confidenceLabel ?text ?coordinates WHERE {
  ?paragraph p:P3 ?statement .
  ?statement pq:P9 ?specialScope ;
             pq:P5 ?confidence ;
             ps:P3 ?text .
  ?specialScope wdt:P2 ?wikidata .
  SERVICE <https://query.wikidata.org/sparql> {
    ?wikidata rwdt:P625 ?coordinates .
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
